apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    productFlavors {
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
    }
}

task launchTests(dependsOn: [":jokend:appengineRun", "test"]) {
    project(":jokend").afterEvaluate { jokend ->
        jokend.extensions.appengine.daemon = true
    }
    doLast {
        println "started the server!"
    }
}

launchTests.finalizedBy ":jokend:appengineStop"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Added for AdMob
    compile project(path: ':androjokes')
    compile project(path: ':getjokes')
    compile 'com.android.support:appcompat-v7:23.3.0'
    freeCompile 'com.google.android.gms:play-services-ads:9.2.0'
    compile project(path: ':jokend', configuration: 'android-endpoints')
}